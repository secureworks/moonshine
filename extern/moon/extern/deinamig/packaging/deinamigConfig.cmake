cmake_minimum_required(VERSION 3.19)

set(deinamig_known_comps static shared object)
set(deinamig_comp_static NO)
set(deinamig_comp_shared NO)
set(deinamig_comp_object NO)
foreach (deinamig_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (deinamig_comp IN_LIST deinamig_known_comps)
        set(deinamig_comp_${deinamig_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
                "deinamig does not recognize component `${deinamig_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if ((deinamig_comp_static AND deinamig_comp_shared) OR (deinamig_comp_object AND deinamig_comp_shared) OR (deinamig_comp_static AND deinamig_comp_object))
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "deinamig `static` and `shared` and `object` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(deinamig_static_targets "${CMAKE_CURRENT_LIST_DIR}/deinamig-static-targets.cmake")
set(deinamig_shared_targets "${CMAKE_CURRENT_LIST_DIR}/deinamig-shared-targets.cmake")
set(deinamig_object_targets "${CMAKE_CURRENT_LIST_DIR}/deinamig-object-targets.cmake")

macro(deinamig_load_targets type)
    if (NOT EXISTS "${deinamig_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
                "deinamig `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${deinamig_${type}_targets}")
endmacro()

if (deinamig_comp_static)
    deinamig_load_targets(static)
elseif (deinamig_comp_shared)
    deinamig_load_targets(shared)
elseif (deinamig_comp_object)
    deinamig_load_targets(object)
elseif (DEFINED DEINAMIG_STATIC_LIBS AND DEINAMIG_STATIC_LIBS)
    deinamig_load_targets(static)
elseif (DEFINED DEINAMIG_SHARED_LIBS AND DEINAMIG_SHARED_LIBS)
    deinamig_load_targets(shared)
elseif (DEFINED DEINAMIG_SHARED_LIBS AND NOT DEINAMIG_SHARED_LIBS)
    deinamig_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${deinamig_shared_targets}")
        deinamig_load_targets(shared)
    elseif (EXISTS "${deinamig_static_targets}")
        deinamig_load_targets(static)
    else ()
        deinamig_load_targets(object)
    endif ()
elseif (BUILD_OBJECT_LIBS)
    if (EXISTS "${deinamig_object_targets}")
        deinamig_load_targets(object)
    elseif (EXISTS "${deinamig_static_targets}")
        deinamig_load_targets(static)
    else ()
        deinamig_load_targets(shared)
    endif ()
else ()
    if (EXISTS "${deinamig_static_targets}")
        deinamig_load_targets(static)
    elseif (EXISTS "${deinamig_object_targets}")
        deinamig_load_targets(object)
    else ()
        deinamig_load_targets(shared)
    endif ()
endif ()
