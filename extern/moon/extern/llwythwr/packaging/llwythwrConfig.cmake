cmake_minimum_required(VERSION 3.19)

set(llwythwr_known_comps static shared object)
set(llwythwr_comp_static NO)
set(llwythwr_comp_shared NO)
set(llwythwr_comp_object NO)
foreach (llwythwr_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (llwythwr_comp IN_LIST llwythwr_known_comps)
        set(llwythwr_comp_${llwythwr_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
                "llwythwr does not recognize component `${llwythwr_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if ((llwythwr_comp_static AND llwythwr_comp_shared) OR (llwythwr_comp_object AND llwythwr_comp_shared) OR (llwythwr_comp_static AND llwythwr_comp_object))
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "llwythwr `static` and `shared` and `object` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(llwythwr_static_targets "${CMAKE_CURRENT_LIST_DIR}/llwythwr-static-targets.cmake")
set(llwythwr_shared_targets "${CMAKE_CURRENT_LIST_DIR}/llwythwr-shared-targets.cmake")
set(llwythwr_object_targets "${CMAKE_CURRENT_LIST_DIR}/llwythwr-object-targets.cmake")

macro(llwythwr_load_targets type)
    if (NOT EXISTS "${llwythwr_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
                "llwythwr `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${llwythwr_${type}_targets}")
endmacro()

if (llwythwr_comp_static)
    llwythwr_load_targets(static)
elseif (llwythwr_comp_shared)
    llwythwr_load_targets(shared)
elseif (llwythwr_comp_object)
    llwythwr_load_targets(object)
elseif (DEFINED LLWYTHWR_STATIC_LIBS AND LLWYTHWR_STATIC_LIBS)
    llwythwr_load_targets(static)
elseif (DEFINED LLWYTHWR_SHARED_LIBS AND LLWYTHWR_SHARED_LIBS)
    llwythwr_load_targets(shared)
elseif (DEFINED LLWYTHWR_SHARED_LIBS AND NOT LLWYTHWR_SHARED_LIBS)
    llwythwr_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${llwythwr_shared_targets}")
        llwythwr_load_targets(shared)
    elseif (EXISTS "${llwythwr_static_targets}")
        llwythwr_load_targets(static)
    else ()
        llwythwr_load_targets(object)
    endif ()
elseif (BUILD_OBJECT_LIBS)
    if (EXISTS "${llwythwr_object_targets}")
        llwythwr_load_targets(object)
    elseif (EXISTS "${llwythwr_static_targets}")
        llwythwr_load_targets(static)
    else ()
        llwythwr_load_targets(shared)
    endif ()
else ()
    if (EXISTS "${llwythwr_static_targets}")
        llwythwr_load_targets(static)
    elseif (EXISTS "${llwythwr_object_targets}")
        llwythwr_load_targets(object)
    else ()
        llwythwr_load_targets(shared)
    endif ()
endif ()
